buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.codehaus.groovy.modules.http-builder:http-builder:0.7.2"
        classpath "org.ajoberstar:grgit:2.3.0"
    }
}


import groovy.json.JsonSlurper
import groovyx.net.http.HTTPBuilder
import org.ajoberstar.grgit.Grgit

import static groovyx.net.http.ContentType.JSON

ext {

    //properties
    weather_id = ""
    weather_result = ""

    //directories
    gitDir = "src/main/resources/gitrepos"
    modelDir = "src/main/resources"
}

class WeatherPlugin implements Plugin<Project> {

    void apply(Project project) {
        //we can include apply "plugin: 'pluginName'" like this
        //project.plugins.apply( 'pluginName' )

        project.tasks.create("credentials").configure {

            File readMe = project.file("$project.modelDir/map.json");
            String contents = readMe.getText("UTF-8")

            def slurper = new JsonSlurper()
            def credentials = slurper.parseText(contents)
            project.weather_id = credentials.id;

        }

        project.tasks.create('weather', WeatherTask).configure {
            dependsOn << ["credentials"]

            developer = "Juan Mendez"
            cities = [
                    [name: "Tehran", countryCode: "IR"],
                    [name: "Guatemala", countryCode: "GT"],
                    [name: "Chicago", countryCode: "US"],
                    [name: "Havana", countryCode: "CU"],
                    [name: "Krakow", countryCode: "PL"],
                    [name: "Managua", countryCode: "NI"],
                    [name: "Caracas", countryCode: "VE"],
                    [name: "Ho Chi Minh", countryCode: "VN"],


            ]
        }

        //accessing another plugin
        //project.task.getByName('pluginName').configure{}
    }
}


apply plugin: WeatherPlugin


import static groovyx.net.http.Method.GET

class WeatherTask extends DefaultTask {

    def http
    def cities
    def developer

    WeatherTask() {
        this.http = new HTTPBuilder('http://api.openweathermap.org')
    }

    @TaskAction
    def printCitiesWeather() {

        println "cities $developer wants to know about their weather"

        cities.each { city ->
            readWeather(city.name, city.countryCode)
        }
    }


    def readWeather(city, countryCode) {

        http.request(GET, JSON) {
            uri.path = '/data/2.5/weather'
            uri.query = [APPID: project.weather_id, q: "$city,$countryCode", units: "Imperial"]

            response.success = { resp, json ->
                project.weather_result = json
                println json
            }
        }

        http.handler.'404' = { resp ->
            println "Access Issue $resp"
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = "3.3"
}